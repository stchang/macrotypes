#lang s-exp turnstile/examples/mlish
(require "../rackunit-typechecking.rkt")

(define (fannkuch [n : Int] -> Int)
  (let ([pi (list->vector 
             (for/list ([i (in-range n)]) i))]
        [tmp (make-vector n)]
        [count (make-vector n)])
    (let loop : Int ([flips 0] [perms 0] [r n])
      #;(when (< perms 30)
        (for ([x (in-vector pi)])
          (display (add1 x)))
        (newline))
      (for ([i (in-range r)])
        (vector-set! count i (add1 i)))
      (let ((flips2 (max (count-flips pi tmp) flips)))
        (let loop2 : Int ([r 1])
          (if (= r n)
              flips2
              (let ((perm0 (vector-ref pi 0)))
                (for ([i (in-range r)])
                  (vector-set! pi i (vector-ref pi (add1 i))))
                (vector-set! pi r perm0)
                (vector-set! count r (sub1 (vector-ref count r)))
                (cond
                 [(<= (vector-ref count r) 0)
                  (loop2 (add1 r))]
                 [else (loop flips2 (add1 perms) r)]))))))))

(define (count-flips [pi : (Vector Int)] [rho : (Vector Int)] -> Int)
  (vector-copy! rho 0 pi)
  (let loop : Int ([i 0])
    (if (= (vector-ref rho 0) 0)
        i
        (begin
          (vector-reverse-slice! rho 0 (add1 (vector-ref rho 0)))
          (loop (add1 i))))))

(define (vector-reverse-slice! [v : (Vector X)] [i : Int] [j : Int] -> Unit)
  (let loop : Unit ([i i] [j (sub1 j)])
    (when (> j i)
      (vector-swap! v i j)
      (loop (add1 i) (sub1 j)))))

(define (vector-swap! [v : (Vector X)] [i : Int] [j : Int] -> Unit)
  (let ((t (vector-ref v i)))
    (vector-set! v i (vector-ref v j))
    (vector-set! v j t)))

(check-type (fannkuch 5) : Int -> 7)
(check-type (fannkuch 6) : Int -> 10)
(check-type (fannkuch 7) : Int -> 16)
(check-type (fannkuch 8) : Int -> 22)
(check-type (fannkuch 9) : Int -> 30)
